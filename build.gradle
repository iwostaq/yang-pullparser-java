//  build.gradle for yang-pullparser-java project

apply plugin: 'java'
apply plugin: 'antlr'

sourceCompatibility = 1.8 
targetCompatibility = 1.8 
List prefixRuntimeExcluded = [ 'antlr-runtime' ];

repositories {
  mavenCentral()
}

dependencies {
  antlr   'org.antlr:antlr4:4.7'
  compile 'org.antlr:antlr4:4.7'
  runtime 'org.antlr:antlr4-runtime:4.7'
  testCompile 'junit:junit:4.12'
}

sourceSets {
  tool {
    java {
      srcDir 'src/tool/java'
      compileClasspath += sourceSets.main.compileClasspath
      compileClasspath += sourceSets.main.output
    }
  }

  main {
    antlr {
      srcDirs 'src/main/antlr'
    }
  }
}

task jarTool(type: Jar) {
  manifest {
    attributes("Main-Class" : "iwostaq.yppj.tool.AppMain",
               "Implementation-Version" : "0.9")
  }
  archiveName = "${project.name}-tool.jar"
  from "${buildDir}/classes/main"
  from "${buildDir}/classes/tool"
  from "${buildDir}/resources/main"

  from configurations.runtime.findAll {
    dep -> !prefixRuntimeExcluded.any { dep.name.startsWith(it) }
  }.collect {
    it.isDirectory() ? it : zipTree(it)
  }
}

generateGrammarSource {
  outputDirectory = file('src/main/java/iwostaq/yppj/g/')  
}

compileJava {
  options.encoding = 'UTF-8'
}

jar {
  dependsOn compileToolJava, jarTool
}

clean {
}

